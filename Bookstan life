algoritmo "RegistroLivrosLidos"
// Disciplina   : [Linguagem e Lógica de Programação] 
// Professor   : Fabricio
// começando o código pelo registro e adicionando os comentários
Tipo
   livro = registro
      titulo: caractere
      autor: caractere
      ano_publicacao: inteiro
      estrelas: inteiro
      genero: caractere
      quantidade_paginas: inteiro
      tempo_leitura: inteiro
      data_inicio_leitura: caractere
      data_fim_leitura: caractere
   fimregistro
// logo após o registro vamos denominar var
var
   livros_lidos: vetor[1..100] de livro
   total_livros: inteiro
   opcao: inteiro

procedimento adicionarLivro()
//aqui temos o inicio onde lê e interage com o usuário
inicio
   se total_livros < 100 entao
      escreval("Informe o título do livro: ")
      leia(livros_lidos[total_livros + 1].titulo)
      escreval("Informe o autor do livro: ")
      leia(livros_lidos[total_livros + 1].autor)
      escreval("Informe o ano de publicação do livro: ")
      leia(livros_lidos[total_livros + 1].ano_publicacao)
      escreval("Dê uma classificação de estrelas (1 a 5): ")
      leia(livros_lidos[total_livros + 1].estrelas)
      escreval("Informe o gênero do livro: ")
      leia(livros_lidos[total_livros + 1].genero)
      escreval("Informe a quantidade de páginas do livro: ")
      leia(livros_lidos[total_livros + 1].quantidade_paginas)
      escreval("Quanto tempo levou para ler o livro (em dias): ")
      leia(livros_lidos[total_livros + 1].tempo_leitura)
      escreval("Informe a data de início da leitura (formato dd/mm/yyyy): ")
      leia(livros_lidos[total_livros + 1].data_inicio_leitura)
      escreval("Informe a data de término da leitura (formato dd/mm/yyyy): ")
      leia(livros_lidos[total_livros + 1].data_fim_leitura)

      total_livros <- total_livros + 1
      escreval("Livro adicionado com sucesso!")
   senao
      escreval("Limite de livros atingido. Não é possível adicionar mais livros.")
   fimse
fimprocedimento
// procedimento amores
procedimento listarLivros()
inicio
   se total_livros > 0 entao
      escreval("Lista de livros lidos:")
      para i de 1 ate total_livros faca
         escreval("Livro ", i, ":")
         escreval("Título: ", livros_lidos[i].titulo)
         escreval("Autor: ", livros_lidos[i].autor)
         escreval("Ano de Publicação: ", livros_lidos[i].ano_publicacao)
         escreval("Estrelas: ", livros_lidos[i].estrelas)
         escreval("Gênero: ", livros_lidos[i].genero)
         escreval("Quantidade de Páginas: ", livros_lidos[i].quantidade_paginas)
         escreval("Tempo de Leitura (dias): ", livros_lidos[i].tempo_leitura)
         escreval("Data de Início de Leitura: ", livros_lidos[i].data_inicio_leitura)
         escreval("Data de Término de Leitura: ", livros_lidos[i].data_fim_leitura)
         escreval("------------------------")
      fimpara
   senao
      escreval("Nenhum livro foi registrado ainda.")
   fimse
fimprocedimento
// aqui temos nosso querido menu esgualepado
procedimento menu()
inicio
   escreval("MENU")
   escreval("1 - Adicionar Livro")
   escreval("2 - Listar Livros")
   escreval("3 - Sair")
   escreval("Escolha uma opção: ")
   leia(opcao)

   escolha opcao
      caso 1:
         adicionarLivro()
      caso 2:
         listarLivros()
      caso 3:
         escreval("Fim do programa.")
      senao
         escreval("Opção inválida. Tente novamente.")
   fimescolha
fimprocedimento

// Início do programa principal
inicio
   total_livros <- 0

   repita
      menu()
   ate opcao = 3
fimalgoritmo
           // cabo :)
